
/* catalog.pure: Another demonstration of the xslt interface. */

/* This program illustrates how to translate an internal data structure to an
   XML document and then format the result as HTML, using an XSLT stylesheet.
   (Sample data and stylesheet pilfered from http://www.w3schools.com.) */

using xml;
using namespace xml, xslt;

/* The dataset. To simplify matters, we just define this as a list here. In a
   real application the data would probably come from a database. */

let data =
  [// title, artist, country, company, price, year
   ["Empire Burlesque","Bob Dylan","USA","Columbia",10.90,1985],
   ["Hide your heart","Bonnie Tyler","UK","CBS Records",9.90,1988],
   ["Greatest Hits","Dolly Parton","USA","RCA",9.90,1982],
   ["Still got the blues","Gary Moore","UK","Virgin records",10.20,1990],
   ["Eros","Eros Ramazzotti","EU","BMG",9.90,1997],
   ["One night only","Bee Gees","UK","Polydor",10.90,1998],
   ["Sylvias Mother","Dr.Hook","UK","CBS",8.10,1973],
   ["Maggie May","Rod Stewart","UK","Pickwick",8.50,1990],
   ["Romanza","Andrea Bocelli","EU","Polydor",10.80,1996],
   ["When a man loves a woman","Percy Sledge","USA","Atlantic",8.70,1987],
   ["Black angel","Savage Rose","EU","Mega",10.90,1995],
   ["1999 Grammy Nominees","Many","USA","Grammy",10.20,1999],
   ["For the good times","Kenny Rogers","UK","Mucik Master",8.70,1995],
   ["Big Willie style","Will Smith","USA","Columbia",9.90,1997],
   ["Tupelo Honey","Van Morrison","UK","Polydor",8.20,1971],
   ["Soulsville","Jorn Hoel","Norway","WEA",7.90,1996],
   ["The very best of","Cat Stevens","UK","Island",8.90,1990],
   ["Stop","Sam Brown","UK","A and M",8.90,1988],
   ["Bridge of Spies","T`Pau","UK","Siren",7.90,1987],
   ["Private Dancer","Tina Turner","UK","Capitol",8.90,1983],
   ["Midt om natten","Kim Larsen","EU","Medley",7.80,1983],
   ["Pavarotti Gala Concert","Luciano Pavarotti","UK","DECCA",9.90,1991],
   ["The dock of the bay","Otis Redding","USA","Atlantic",7.90,1987],
   ["Picture book","Simply Red","EU","Elektra",7.20,1985],
   ["Red","The Communards","UK","London",7.80,1987],
   ["Unchain my heart","Joe Cocker","USA","EMI",8.20,1987]];

/* The stylesheet. */

let style = load_stylesheet "catalog.xsl";

/* Translate the data to an XML document. */

using system;

create_cd root [title,artist,country,company,price,year]
		= do (add_last node)
		  [element_text "title"   [] [] title,
		   element_text "artist"  [] [] artist,
		   element_text "country" [] [] country,
		   element_text "company" [] [] company,
		   element_text "price"   [] [] (sprintf "%0.2f" price),
		   element_text "year"    [] [] (sprintf "%d" year)]
		    when node = add_last root (element "cd" [] []) end;

create_doc data	= catalog
		    when
		      catalog = new_doc "1.0" () (element "catalog" [] []);
		      do (create_cd (root catalog)) data;
		    end;

let catalog = create_doc data;

/* Test the resulting XML. Prints the XML data on stdout. */

test		= save_file "-" catalog "" 0;

/* Apply the stylesheet to the data and save the resulting html in a file.
   Just invoke the html function and open catalog.html in your favourite
   browser to view the result. */

html		= save_result_file "catalog.html" res style 0
		    when res = apply_stylesheet style catalog [] end;
