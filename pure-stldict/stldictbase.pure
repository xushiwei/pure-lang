
/* Copyright (c) 2011 by Albert Graef <Dr.Graef@t-online.de>.

   This file is part of the Pure standard library.

   The Pure standard library is free software: you can redistribute it and/or
   modify it under the terms of the GNU Lesser General Public License as
   published by the Free Software Foundation, either version 3 of the License,
   or (at your option) any later version.

   Pure is distributed in the hope that it will be useful, but WITHOUT ANY
   WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
   FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
   more details.

   You should have received a copy of the GNU Lesser General Public License
   along with this program.  If not, see <http://www.gnu.org/licenses/>. */

/* Generic stldict types and corresponding predicates. stldict denotes any
   single-valued dictionary (i.e., hashdict and orddict), stlmdict the
   multi-valued dictionaries (hashmdict and ordmdict) and stlxdict any type of
   dictionary. Note that this module only contains the common definitions
   shared by hashed and ordered dictionaries, you still have to import the
   implementations that you need (hashdict.pure, orddict.pure, or stldict.pure
   if you want the whole shebang). */

type stldict;
type stlmdict;
type stlxdict x::stldict | stlxdict x::stlmdict;

stldictp x  = case x of _::stldict = true; _ = false end;
stlmdictp x = case x of _::stlmdict = true; _ = false end;
stlxdictp x = case x of _::stlxdict = true; _ = false end;

/* Generic dictionary types supported by this implementation. */

type hashxdict;
type ordxdict;

/* Generic coercions for mixed operations. These always convert the operands
   to the most general type (hashed and/or multi-valued). */

namespace stldict with
// Actual conversion functions defined in the hashdict and orddict modules.
public orddict ordmdict hashdict hashmdict;
coerce f x y = f (c x) (c y) when
  c = {orddict, ordmdict; hashdict, hashmdict} !
    (typep hashxdict x || typep hashxdict y, stlmdictp x || stlmdictp y);
end;
// Check in advance whether any conversions need to be done. This kludge is
// necessary to prevent infinite recursion, because the rules below are
// usually imported before the actual operations are defined.
coercep x y = typep hashxdict x ~= typep hashxdict y ||
  stlmdictp x ~= stlmdictp y;
end;

// Make the comparison and set operations work with any mix of operands.
x::stlxdict == y::stlxdict = stldict::coerce (==) x y if stldict::coercep x y;
x::stlxdict ~= y::stlxdict = stldict::coerce (~=) x y if stldict::coercep x y;
x::stlxdict <= y::stlxdict = stldict::coerce (<=) x y if stldict::coercep x y;
x::stlxdict >= y::stlxdict = stldict::coerce (>=) x y if stldict::coercep x y;
x::stlxdict <  y::stlxdict = stldict::coerce (<)  x y if stldict::coercep x y;
x::stlxdict >  y::stlxdict = stldict::coerce (>)  x y if stldict::coercep x y;
x::stlxdict +  y::stlxdict = stldict::coerce (+)  x y if stldict::coercep x y;
x::stlxdict -  y::stlxdict = stldict::coerce (-)  x y if stldict::coercep x y;
x::stlxdict *  y::stlxdict = stldict::coerce (*)  x y if stldict::coercep x y;
