/* uts_stlmmap_iter.pure -- tests for stlmmap iterators */
//-- --comment c_off --verbose off

//*** Imports *********************************************************

using system, stlmmap, stlvec;
//- ()

/*** Iterators - Construction ****************************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i = stl::find sm1 "0"; stl::endp i;
//- 1

let i = stl::find sm1 "a"; get i;
//- "a"=>1

let i = stl::find sm1 "z"; stl::endp i;
//- 1

let i = stl::l_bound sm1 "0"; get i;
//- "a"=>1

let i = stl::l_bound sm1 "a";  get i;
//- "a"=>1

let i = stl::l_bound sm1 "bb"; get i;
//- "c"=>3

let i = stl::l_bound sm1 "dd"; get i;
//- "e"=>5

let i = stl::l_bound sm1 "e";  get i;
//- "e"=>5

let i = stl::l_bound sm1 "z"; stl::endp i;
//- 1

let i = stl::l_bound sm1 "f"; stl::endp i;
//- 1

let i = stl::u_bound sm1 "0"; get i;
//- "a"=>1

let i = stl::u_bound sm1 "a";  get i;
//- "b"=>2

let i = stl::u_bound sm1 "bb"; get i;
//- "c"=>3

let i = stl::u_bound sm1 "dd"; get i;
//- "e"=>5

let i = stl::u_bound sm1 "e"; stl::endp i;
//- 1

let i = stl::u_bound sm1 "z"; stl::endp i;
//- 1

let i, j = stl::lu_bounds sm1 "0"; (get i,get j);
//- "a"=>1,"a"=>1

let i, j = stl::lu_bounds sm1 "a"; (get i,get j);
//- "a"=>1,"b"=>2

let i, j = stl::lu_bounds sm1 "b"; (get i,get j);
//- "b"=>2,"c"=>3

let i, j = stl::lu_bounds sm1 "bb"; (get i,get j);
//- "c"=>3,"c"=>3

let i, j = stl::lu_bounds sm1 "dd"; (get i,get j);
//- "e"=>5,"e"=>5

let i, j = stl::lu_bounds sm1 "e"; (get i,stl::endp j);
//- "e"=>5,1

let i, j = stl::lu_bounds sm1 "z"; (stl::endp i,stl::endp j);
//- 1,1


let k = stl::iterator j; (stl::endp k, j==k, j===k);
//- 1,1,0

get (stl::dec k); (get k, j==k);
//- "e"=>5,0

/*** Info **************************************************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i_begin, i_pastend = stl::begin sm1, stl::pastend sm1;
//- ()

let i_a = stl::find sm1 "a"; get i_a;
//- "a"=>1

let i_c = stl::find sm1 "c"; get i_c;
//- "c"=>3

stl::beginp i_begin, stl::beginp i_pastend;
//- 1,0

stl::beginp i_a, stl::beginp i_c;
//- 1,0

stl::endp i_begin, stl::endp i_pastend;
//- 0,1

stl::endp i_a, stl::endp i_c;
//- 0,0

let ok, smx, k, v = stl::get_info i_begin; (ok, smx===sm1, k, v);
//- 1,1,stl::smbeg,1

let ok, smx, k, v = stl::get_info i_a; (ok, smx===sm1, k, v);
//- 1,1,stl::smbeg,1

let ok, smx, k, v = stl::get_info i_c; (ok, smx===sm1, k, v);
//- 1,1,"c",3

let ok, smx, k, v = stl::get_info i_pastend; (ok, smx===sm1, k, v);
//- 1,1,stl::smend,[]

/*** Element Access and Modification *******************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i_begin, i_pastend = stl::begin sm1, stl::pastend sm1;
//- ()

let i_a = stl::find sm1 "a"; get i_a;
//- "a"=>1

let i_c = stl::find sm1 "c"; get i_c;
//- "c"=>3

get i_begin, stl::get_key i_begin, stl::get_val i_begin;
//- "a"=>1,"a",1

get i_a, stl::get_key i_a, stl::get_val i_a;
//- "a"=>1,"a",1

get i_c, stl::get_key i_c, stl::get_val i_c;
//- "c"=>3,"c",3

catch id $ get i_pastend;
//- out_of_bounds

catch id $ stl::get_key i_pastend;
//- out_of_bounds

catch id $ stl::get_val i_pastend;
//- out_of_bounds

put i_begin 11; stl::get_val i_begin;
//- 11

put i_a 21; stl::get_val i_a;
//- 21

put i_c 33; stl::get_val i_c;
//- 33

catch id $ put i_pastend 42;
//- out_of_bounds

/*** Navigation *******************************************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i_b, i_e = stl::begin sm1, stl::pastend sm1;
//- ()

let i = stl::begin sm1;
//- ()

get i, stl::get_key i, stl::get_val i;
//- "a"=>1,"a",1

put i 0, get i;
//- 0,"a"=>0

stl::inc i; get i;
//- "b"=>2

stl::inc i; get i;
//- "c"=>3

stl::inc i; get i;
//- "d"=>4

stl::inc i; get i;
//- "e"=>5

stl::inc i; stl::endp i;
//- 1

stl::inc i; stl::endp i;
//- 1

catch id $ get i;
//- out_of_bounds

stl::dec i; get i;
//- "e"=>5

stl::dec i; get i;
//- "d"=>4

stl::dec i; get i;
//- "c"=>3

stl::dec i; get i;
//- "b"=>2

stl::dec i; get i;
//- "a"=>0

catch id $ stl::dec i;
//- out_of_bounds

stl::move i 4; get i;
//- "e"=>5

stl::move i 100; stl::endp i;
//- 1

stl::move i (-1); get i;
//- "e"=>5

catch id $ stl::move i (-100);
//- out_of_bounds

//*** Iterator Operators *************************************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i = sm1^"a"; get i;
//- "a"=>1

members sm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

let j = sm1^"a"; get j;
//- "a"=>1

members sm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

let k = sm1^"z"; get k;
//- "z"=>[]

members sm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"z"=>[]]

i == j;
//- 1

i == i;
//- 1

i == k;
//- 0

i == stl::begin sm1;
//- 1

i == stl::pastend sm1;
//- 0

stl::begin sm1 == stl::pastend sm1;
//- 0

let i1 = i; i1 == i;
//- 1

/*** Iterator Ranges *************************************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i_begin, i_pastend = stl::begin sm1, stl::pastend sm1;
//- ()

let i_a = stl::find sm1 "a"; get i_a;
//- "a"=>1

let i_c = stl::find sm1 "c"; get i_c;
//- "c"=>3

let ok, smx, f, l = stl::range_info (i_begin,i_begin); ok;
//- 1

smx===sm1, f===i_begin, l===i_begin, f==i_begin, l==i_begin;
//- 1,0,0,1,1

let ok, smx, f, l = stl::range_info (i_a,i_begin); ok;
//- 1

smx===sm1, f===i_a, l===i_begin, f==i_a, l==i_begin;
//- 1,0,0,1,1

let ok, smx, f, l = stl::range_info (i_c,i_begin); ok;
//- 0

let ok, smx, f, l = stl::range_info (i_begin,i_pastend); ok;
//- 1

smx===sm1, f===i_begin, l===i_pastend, f==i_begin, l==i_pastend;
//- 1,0,0,1,1

let ok, smx, f, l = stl::range_info (i_a,i_c); ok;
//- 1

smx===sm1, f===i_a, l===i_c, f==i_a, l==i_c;
//- 1,0,0,1,1

members (i_begin,i_begin);
//- []

members (i_begin,i_pastend);
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

catch id $ members (i_pastend,i_begin);
//- bad_argument

members (i_pastend,i_pastend);
//- []

members (i_a,i_a);
//- []

members (i_a,i_c);
//- ["a"=>1,"b"=>2]

catch id $ members (i_c,i_a);
//- bad_argument

members (i_c,i_c);
//- []

members (i_begin,i_a);
//- []

members (i_a,i_begin);
//- []

members (i_a,i_pastend);
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

catch id $ members (i_pastend,i_a);
//- bad_argument

members (i_begin,i_c);
//- ["a"=>1,"b"=>2]

catch id $ members (i_c,i_begin);
//- bad_argument

members (i_c,i_pastend);
//- ["c"=>3,"d"=>4,"e"=>5]

catch id $ members (i_pastend,i_c);
//- bad_argument

/*** Erroneous Ranges **************************************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i1_begin, i1_pastend = stl::begin sm1, stl::pastend sm1;
//- ()

let i1_a, i1_c = stl::find sm1 "a", stl::find sm1 "c";
//- ()

let sm2 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i2_begin, i2_pastend = stl::begin sm2, stl::pastend sm2;
//- ()

let i2_a, i2_c = stl::find sm2 "a", stl::find sm2 "c";
//- ()

stl::range_info (i1_pastend,i1_begin);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

stl::range_info (i1_pastend,i1_a);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

stl::range_info (i1_c,i1_begin);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

stl::range_info (i1_c,i1_a);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

stl::range_info (i1_begin,i2_begin);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

stl::range_info (i1_pastend,i2_pastend);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

stl::range_info (i1_a,i2_a);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

stl::range_info (i1_c,i2_c);
//- 0,#<pointer 0>,#<pointer 0>,#<pointer 0>

erase (sm1,i1_c); members sm1;
//- ["a"=>1,"b"=>2,"d"=>4,"e"=>5]

let ok, smx, k, v = stl::get_info i1_c; ok, smx===sm1;
//- 0,1

catch id $ members (i1_c,i1_pastend);
//- bad_argument

catch id $ erase (sm2,i1_c);
//- bad_argument

/*** Key Based Ranges *************************************************/

let sm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5];
//- ()

let i_begin, i_pastend = stl::begin sm1, stl::pastend sm1;
//- ()

let i_a, i_b, i_c = stl::find sm1 "a", stl::find sm1 "b", stl::find sm1 "c";
//- ()

let i_a = stl::find sm1 "a"; get i_a;
//- "a"=>1

let ok, smx, f, l = stl::range_info (sm1,stl::smbeg,stl::smbeg); ok;
//- 1

smx===sm1, f==i_begin, l==i_begin;
//- 1,1,1

let ok, smx, f, l = stl::range_info (sm1,stl::smbeg,stl::smend); ok;
//- 1

smx===sm1, f==i_begin, l==i_pastend;
//- 1,1,1

let ok, smx, f, l = stl::range_info (sm1,"a","c"); ok;
//- 1

smx===sm1, f==i_a, f==i_begin, l==i_c;
//- 1,1,1,1

let ok, smx, f, l = stl::range_info (sm1,"0","z"); ok;
//- 1

smx===sm1, f==i_begin, l==i_pastend;
//- 1,1,1

let ok, smx, f, l = stl::range_info (sm1,"0","0"); ok;
//- 1

smx===sm1, f==i_begin, l==i_begin;
//- 1,1,1

let ok, smx, f, l = stl::range_info (sm1,"z","z"); ok;
//- 1

smx===sm1, f==i_pastend, l==i_pastend;
//- 1,1,1

let ok, smx, f, l = stl::range_info (sm1,"c","a"); ok;
//- 1

smx===sm1, f==i_c, l==i_c;
//- 1,1,1

let ok, smx, f, l = stl::range_info (sm1,"b"); ok;
//- 1

smx===sm1, f==i_b, l==i_c;
//- 1,1,1

