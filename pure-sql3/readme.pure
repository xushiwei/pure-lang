/* readme.pure -- examples in README */


using sql3; using namespace sql3;

let dbp = open "readme.db";

exec dbp "create table if not exists RM (name text, age integer)";

exec dbp "delete from RM";

let sp1 = prep dbp "ci" "insert into RM values (?,?)";

exec sp1 ("Sam",20);

exec sp1 ("Fred",22);

let sp2 = prep dbp "ci:i" "select * from RM where age > ?";

exec sp2 18;

let sp3 = prep dbp "c:" "select name from RM";

exec sp3 ();

db_error_handler (db_error ec msg) = ()
when
  source = if ec > 0 then "SQLite" else "Sql3";
  printf "%s db_error: ec %d, %s\n" (source,ec,msg);
end;

db_error_handler x = throw x;

catch db_error_handler (exec dbp "select * from NO_TABLE");

function dbp "p_fn" 2 plus with plus x y = x + y; end;

let sp4 = prep dbp "cii:" 
         "select p_fn('Hi ',name), age, p_fn(age,10) from RM";

exec sp4 ();


function dbp "p_qm" (-1) quasimodo with
  quasimodo xs = "quasimodo: "+join ":" [str x | x=xs];
end;


function dbp "p_count" 0 counter with
  counter () = put r (get r+1);
end when r = ref 0 end;

let sp5 = prep dbp "ic:" "select p_count(), p_qm(name,age) from RM";

//exec sp5 ();


function dbp "p_avg" 1 (step,final,(0,0.0)) with
  step (n,a) x = n+1, a+x;
  final (n,a) = a/n;
end;

let sp6 = prep dbp "id:" "select count(name), p_avg(age) from RM";

exec sp6 ();

